//
// Implements a circular buffer that holds CAN messages waiting to go out on the CAN
// physical layer.

unit CanBufferOut;

uses
  HelperCAN,
  MCU_Setup_dsPIC33EPxxxGP502;

// Called one on startup to initialize internal variables
procedure CanBufferOut_Initialize;
// Returns the next empty buffer to place another message to send on the CAN, if there is no free buffers it return nil
function CanBufferOut_NextEmpty: POpenLCBCanBuffer;
// Returns the next buffer to send on the CAN, if there is nothing to send it returns false and CanBuffer is undefined
// The result is a COPY of what was in the buffer, the buffer is released before this function returns with the copy
function CanBufferOut_NextFull(var CanBuffer: TOpenLCBCanBuffer): Boolean;
  
type
  TCanOutBuffer = record
    Buffer: array[CAN_OUT_BUFFER_MAX] of TOpenLCBCanBuffer;
    iHead,
    iTail: Integer;
    MaxCount: Integer;
    Count: Integer;
    TotalCount: DWord;
    Overrun: Boolean;
  end;
  
var
  CanOut: TCanOutBuffer;
  
implementation

procedure CanBufferOut_Initialize;
var
  i, j: Integer;
begin
  for i := 0 to CAN_OUT_BUFFER_MAX - 1 do
  begin
    CanOut.Buffer[i].MTI := 0;
    CanOut.Buffer[i].PayLoadCount := 0;
    for j := 0 to 7 do
      CanOut.Buffer[i].Payload[j] := 0;
  end;
  CanOut.iHead := 0;
  CanOut.iTail := 0;
  CanOut.MaxCount := 0;
  CanOut.Count := 0;
  CanOut.TotalCount := 0;
  CanOut.Overrun := False;
end;

function CanBufferOut_NextEmpty: POpenLCBCanBuffer;
begin
  Result := nil;
  if CanOut.Count < CAN_OUT_BUFFER_MAX - 1 then
  begin
    // Pickup the next head
    Result := @CanOut.Buffer[CanOut.iHead];
    // Move to the next Head
    Inc(CanOut.iHead);
    // Check for Rollover
    if CanOut.iHead >= CAN_OUT_BUFFER_MAX then
      CanOut.iHead := 0;
    // Update Count used
    Inc(CanOut.Count);
    // Update the largest Count used
    if CanOut.Count > CanOut.MaxCount then
      CanOut.MaxCount := CanOut.Count;
    // Increase the running total
    Inc(CanOut.TotalCount);
  end
end;


function CanBufferOut_NextFull(var CanBuffer: TOpenLCBCanBuffer): Boolean;
var
  i: Integer;
begin
  Result := False;
  CanBuffer.MTI := 0;  // Known value
  if CanOut.iHead <> CanOut.iTail then
  begin
    CanBuffer.MTI := CanOut.Buffer[CanOut.iTail].MTI;
    CanBuffer.PayloadCount := CanOut.Buffer[CanOut.iTail].PayloadCount;
    for i := 0 to 7 do
      CanBuffer.Payload[i] := CanOut.Buffer[CanOut.iTail].Payload[i];
    Inc(CanOut.iTail);
    if CanOut.iTail >= CAN_OUT_BUFFER_MAX then
      CanOut.iTail := 0;
    Dec(CanOut.Count);
    Result := True;
  end
end;

end.