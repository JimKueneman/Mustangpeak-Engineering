program DoublePulse;

{ Declarations section }

{$DEFINE UART}

const
  {****** MCU Setup Parameters ******}
  // Frequency the CPU is running
  OSCILLATORFREQ = 32e6;
  // How many cycles of the Oscillator for each master timer cycle
  CLOCKDIVIDER = 2;
  // Frequency of the master timer cloc
  CLOCK_FREQ = real( OSCILLATORFREQ/CLOCKDIVIDER);
  // The period of a single clock timer tick (in seconds)
  CLOCK_PERIOD = real( 1/CLOCK_FREQ);
  
  {****** Engine Parameters *******}
  // Engine cycles
  ENGINE_CYCLE = 4;
  // Number of Cylinders (assuming not a dual point distributor)
  CYLINDERS = 8;
  // Revolutions per Cycle
  REV_PER_CYCLE = ENGINE_CYCLE/2;
  
  { ****** Sofer parameters to define limits for decision logic in the program ******}
  // Below this and we say we stalled and restart the Statemachine
  STALL_RPM = 50;
  // Above this and it is a bad measurement of RPM
  MAX_RPM = 7000;
  // Above this and we move to the Running State
  MIN_RUNNING_RPM = 400;
  // How many fake RPM triggers to give the FiTech on startup
  FAKE_RPM_COUNT = 0;
  // What RPM to use for the Fake RPM signal
  FAKE_RPM = real( 7000);
  // Smoothing RPM Filter Depth and How many valid RPM spark pulses detected in the pin change notification to
  // move on in the Stat machine before moving out of Cranking
  RPM_FILTER_AND_STABLE_CRANK_PULSE_LEN = 8;
  // How much of a distributor spark period to blank out before looking for the next pin change notification
  BLANK_PERCENT = 0.35;
  // How long between pin change notification before we claim the engine has stalled
  STALL_DISTRIBUTOR_PERIOD = real( 60 * REV_PER_CYCLE /(STALL_RPM*CYLINDERS));
  // Time to blank if the RPM made no sense at all during cranking
  DEFAULT_CRANKING_RPM = 200;
  DEFAULT_CRANKING_BLANK_PERIOD = real( 60 * REV_PER_CYCLE/(DEFAULT_CRANKING_RPM*CYLINDERS));
  
  // Statemachine states
  STATE_STALLED = 0;               // Engine is not running
  STATE_STABLIZE_CRANKING = 1;     // Engine is cranking but waiting for the a stable set of pin change notification to occur
  STATE_FAKE_RPM = 2;              // Send the FiTech some fake higher RPM signals to get it to shoot fuel sooner
  STATE_CRANKING = 3;              // Cranking normally but not averaging the pin change notifications yet
  STATE_RUNNING = 4;               // RPM indicates we started so start averaging the pin change notification for the FiTech Output
  
  TACH_LO = 1;                     // Pin output for a Tach Lo output (common collector amplifier)
  TACH_HI = 0;                     // Pin output for a Tach Hi output (common collector amplifier)

  COILNEG = B0;    // Bit 0        // The Pin index in the associated Port for the Coil Negative Input
  TACHOUT = B9;    // Bit 9        // The Pin index in the associated Port for the Tach Output
  
var
  CoilNeg_Port: Word at PORTA;
  CoilNeg_Pin: sbit at LATA0_bit;
  CoilNeg_TRIS: sbit at TRISA0_bit;
  
  TachPort: Word at PORTB;
  TachOut_Pin: sbit at LATB9_bit;
  TachOut_TRIS: sbit at TRISB9_bit;
  
  HeartBeat_Port: Word at PORTA;
  HeartBeat_Pin: sbit at LATA3_bit;
  HeartBeat_TRIS: sbit at TRISA3_bit;
  
  Debug_Port: Word at PORTA;
  Debug_Pin: sbit at LATA2_bit;
  Debug_TRIS: sbit at TRISA2_bit;
  
  // RPM Averaging array and index counter
  Distributor_Period_Filter_Array: array[RPM_FILTER_AND_STABLE_CRANK_PULSE_LEN] of real;
  Distributor_Period_Filter_Counter: Integer;
  
  State,
  Stable_Cranking_RPM_Count_Global: Integer;
  Last_RPM_Global: real;
  
{$IFDEF UART}
procedure PrintState;
var
  Buffer: array[6] of char;
begin
  IntToStr(State, Buffer);
  UART_Write_Text(Buffer);
  UART1_Write(#13);
end;         
{$ENDIF}
// Converts an RPM into a Period Time between each cyclinder firing in Seconds
function RPM_To_Distributor_Period(RPM: real): real;
begin
  Result := 0.0;
  if RPM > 0 then
    Result := real( 60 * REV_PER_CYCLE / (RPM * CYLINDERS ));
end;

// Converts a Period of Time between cylinder firings into RPM
function Distributor_Period_To_RPM(Distributor_Period: real): real;
begin
  Result := 0.0;
  if Distributor_Period > 0.0 then
    Result := real( 60 / Distributor_Period / CYLINDERS * REV_PER_CYCLE)
end;

// Converts a DWORD Timer Count to actual elapsed time (seconds) scaled by the Timer Divider (used 1, 8, 32, 64, etc)
function TimerCounts_To_Time(TimerCounts: DWORD; Divider: Word): real;
begin
  Result := real( TimerCounts) * CLOCK_PERIOD ///* real( Divider);
end;

// Converts time (seconds) to a DWORD Timer Count using the passed Timer Divider (1, 8, 32, 64, etc)
function Time_To_TimerCounts(Time: real; Divider: Word): DWORD;
begin
  Result := DWORD( Time / (CLOCK_PERIOD * real( Divider)))
end;

procedure UpdateDistributorArray(NewDistributorTime: real);
begin
  // Insert the lastest item in the array
  Distributor_Period_Filter_Array[Distributor_Period_Filter_Counter] := NewDistributorTime;
  Inc(Distributor_Period_Filter_Counter);
  // Update the array counter
  if Distributor_Period_Filter_Counter >= RPM_FILTER_AND_STABLE_CRANK_PULSE_LEN then
    Distributor_Period_Filter_Counter := 0;
end;

// Sends the delayed blanking pulse to the FiTech and stalls the CN Interrupt for the blanking
procedure TriggerOutput_With_Wait(BlankingTime: real);
begin
  TachOut_Pin := TACH_Hi;
  Delay_Cyc_Long( Time_To_TimerCounts(BlankingTime * BLANK_PERCENT, 1));  // Just a bit more than 15 degrees dwell (out of 45
  TachOut_Pin := TACH_LO;
end;

// Calculates the average of the items in the Distributor Period Array
function AverageDistributorPeriod: real;
var
  i: Integer;
begin
  Result := 0.0;
  for i := 0 to RPM_FILTER_AND_STABLE_CRANK_PULSE_LEN - 1 do
    Result := Result + Distributor_Period_Filter_Array[i];
  Result := Result / RPM_FILTER_AND_STABLE_CRANK_PULSE_LEN
end;

procedure interrupt_CCT1_RPM_Measurment_And_Stall_Timer(); iv IVT_ADDR_CCT1INTERRUPT;    // Interrupt for the HiWord Compare 1 (16Bit mode/32Bit mode)
begin
  // Stop the RPM Timer
  CCPON_CCP1CON1L_bit := 0;
  // Reset the RPM/Stall Timer; Keep looking for stalls as that is the only way to enter this
  CCP1TMRH := $0000;
  CCP1TMRL := $0000;
  CCT1IF_bit := 0;
  // Start the RPM Timer
  CCPON_CCP1CON1L_bit := 1;
  State := STATE_STALLED;
  {$IFDEF UART}
  UART1_Write_Text('Stall Detected IRQ');
  UART1_Write(#13);
  {$ENDIF}
end;

     {
procedure interrupt_CCT2_Stall_Timer(); iv IVT_ADDR_CCT2INTERRUPT;    // Interrupt for the HiWord Compare 2 (16Bit mode/32Bit mode)
begin
  // Called if the CN interrupt is not called fast enough
  State := STATE_STALLED;
  Debug_Pin := not Debug_Pin;
  CCT2IF_bit := 0;
end;

procedure interrupt_Timer1(); iv IVT_ADDR_T1INTERRUPT;
begin
  T1IF_bit := 0;             // clear T1IF
end;
 }
 
procedure interrupt_PinChangeNotification(); iv IVT_ADDR_CNINTERRUPT;
var
  PortCopy: Word;
  i: Integer;
  LastDistributorPeriod: real;
  DistributorPeriodTimerCount: DWord;
  StrBuffer: array[23] of char;
begin
  PortCopy := CoilNeg_Port;    // Clear all changes
  // Detect rising edges only
  if PortCopy.COILNEG = 1 then
  begin
    // Stop the RPM Timer
    CCPON_CCP1CON1L_bit := 0;
    // Capture it contents
    HiWord(DistributorPeriodTimerCount) := CCP1TMRH;
    LoWord(DistributorPeriodTimerCount) := CCP1TMRL;
    // Reset the RPM/Stall Timer
    CCP1TMRH := $0000;
    CCP1TMRL := $0000;   
    // Makes sure it can't jump to the stall interrupt
    CCT1IF_bit := 0;
    // Start the RPM Timer for the next Cylinder Cycle
    CCPON_CCP1CON1L_bit := 1;
    
    if State = STATE_STALLED then
    begin
      Last_RPM_Global := 0.0;
      LastDistributorPeriod := 0.0;
    end else
    begin
      LastDistributorPeriod := TimerCounts_To_Time(DistributorPeriodTimerCount, 1);
      Last_RPM_Global := Distributor_Period_To_RPM(LastDistributorPeriod);
    end;

    {
    FloatToStr(Last_RPM_Global, StrBuffer);
    UART1_Write_Text(StrBuffer);
    UART1_Write(#13);
    }
    
    case State of
      STATE_STALLED :
        begin
          Stable_Cranking_RPM_Count_Global := 0;
          Distributor_Period_Filter_Counter := 0; 
          Last_RPM_Global := 0;
          {$IFDEF UART}PrintState;{$ENDIF}
          State := STATE_STABLIZE_CRANKING;
          {$IFDEF UART}PrintState;{$ENDIF}
        end;
      STATE_STABLIZE_CRANKING :
        begin
          // Decision point for if this was a valid signal
          if (Last_RPM_Global > STALL_RPM) and (Last_RPM_Global < MAX_RPM) then  // Are we cranking in a stable place yet?
          begin
            UpdateDistributorArray(LastDistributorPeriod);
            Delay_Cyc_Long( Time_To_TimerCounts(LastDistributorPeriod*BLANK_PERCENT, 1));
            Inc(Stable_Cranking_RPM_Count_Global)
          end else
          begin
            Delay_Cyc_Long( Time_To_TimerCounts(DEFAULT_CRANKING_BLANK_PERIOD*BLANK_PERCENT, 1));
            Dec(Stable_Cranking_RPM_Count_Global);
          end;
          // Did we reach the correct number of stable pulses to move on?
          if Stable_Cranking_RPM_Count_Global >= RPM_FILTER_AND_STABLE_CRANK_PULSE_LEN then
          begin        
            State := STATE_FAKE_RPM;
            {$IFDEF UART}PrintState;{$ENDIF}
          end;
        end;
      STATE_FAKE_RPM :
        begin
          for i := 0 to FAKE_RPM_COUNT -1 do
          begin
            TachOut_Pin := TACH_Hi;
            Delay_Cyc_Long( Time_To_TimerCounts(RPM_To_Distributor_Period(FAKE_RPM*BLANK_PERCENT), 1));
            TachOut_Pin := TACH_Lo;
            Delay_Cyc_Long( Time_To_TimerCounts(RPM_To_Distributor_Period(FAKE_RPM*(1-BLANK_PERCENT)), 1));
          end;
          State := STATE_CRANKING;
          {$IFDEF UART}PrintState;{$ENDIF}
        end;
      STATE_CRANKING :
        begin
          UpdateDistributorArray(LastDistributorPeriod);
          TriggerOutput_With_Wait(LastDistributorPeriod);
          if Last_RPM_Global > MIN_RUNNING_RPM then
          begin
            State := STATE_RUNNING;
            {$IFDEF UART}PrintState;{$ENDIF}
          end;
        end;
      STATE_RUNNING :
        begin
          UpdateDistributorArray(LastDistributorPeriod);
          TriggerOutput_With_Wait(LastDistributorPeriod);
      end else
        State := STATE_STALLED;     // State is messed up, start over
    end;    // Case
  end;

  // Some free debounce guarding by putting this at the end
  CNIF_bit := 0;
end;

var
  StrBuffer: array[23] of Char;
begin
  { Main program }
  ANSA := 0;        // Make them all Digital
  ANSB := 0;

  State := STATE_STALLED;

  TachOut_Pin := TACH_LO;   // Set it low
  HeartBeat_Pin := 0;
  Debug_Pin := 0;
  
  CoilNeg_TRIS := 1;   // Input
  TachOut_TRIS := 0;   // Tach Output
  HeartBeat_TRIS := 0; // Output
  Debug_TRIS := 0;
  
  // Clock Setup
  CLKDIV := 0;                      // Set both dividers to 1:1 and don't use the DOZE to reset dividers (power saving feature)

  // Compare Module Setup as an Event Timer
  CCPON_CCP1CON1L_bit := 0;  // Off
  TRIGEN_CCP1CON1H_bit := 0;
  SYNC_0_CCP1CON1H_bit := 0;
  SYNC_1_CCP1CON1H_bit := 0;
  SYNC_2_CCP1CON1H_bit := 0;
  SYNC_3_CCP1CON1H_bit := 0;
  SYNC_4_CCP1CON1H_bit := 0;
  TMR32_CCP1CON1L_bit := 1; // 32 Bit Timers
  CLKSEL_0_CCP1CON1L_bit := 0;
  CLKSEL_1_CCP1CON1L_bit := 0;
  CLKSEL_2_CCP1CON1L_bit := 0;
  TMRPS_0_CCP1CON1L_bit := 0;
  TMRPS_1_CCP1CON1L_bit := 0;  // Divide by 1
  CCP1TMRH := $0000;
  CCP1TMRL := $0000;
  CCP1PRL := LoWord( DWORD(STALL_DISTRIBUTOR_PERIOD/CLOCK_PERIOD));
  CCP1PRH := HiWord( DWORD(STALL_DISTRIBUTOR_PERIOD/CLOCK_PERIOD));
  CCT1IP_0_bit := 1;
  CCT1IP_1_bit := 1;
  CCT2IP_2_bit := 0;
  CCT1IF_bit := 0;
  CCT1IE_bit := 1;
  CCPON_CCP1CON1L_bit := 1; // ON, start looking for a stall

{
  // Compare Module Setup as an Event Timer
  TRIGEN_CCP2CON1H_bit := 0;
  SYNC_0_CCP2CON1H_bit := 0;
  SYNC_1_CCP2CON1H_bit := 0;
  SYNC_2_CCP2CON1H_bit := 0;
  SYNC_3_CCP2CON1H_bit := 0;
  SYNC_4_CCP2CON1H_bit := 0;
  TMR32_CCP2CON1L_bit := 1; // 32 Bit Timers
  CLKSEL_0_CCP2CON1L_bit := 0;
  CLKSEL_1_CCP2CON1L_bit := 0;
  CLKSEL_2_CCP2CON1L_bit := 0;
  TMRPS_0_CCP2CON1L_bit := 0;
  TMRPS_1_CCP2CON1L_bit := 0;  // Divide by 1
  CCP2PRL := LoWord( DWORD(STALL_PERIOD/CLOCK_PERIOD*REV_PER_CYCLE));
  CCP2PRH := HiWord( DWORD(STALL_PERIOD/CLOCK_PERIOD*REV_PER_CYCLE));
  CCT2IF_bit := 0;       //  CCT for the High Word in 32 Bit
  CCT2IE_bit := 1;       //  CCT for the High Word in 32 Bit
  CCT2IP_1_bit := 1;     // Low Priority, at least lower than the CN
  CCT2IP_2_bit := 0;
  CCT2IP_2_bit := 0;
  CCPON_CCP2CON1L_bit := 1;

  // Timer Setup
  TMR1 := 0;
  TON_bit := 0;                // OFF
  TCKPS_0_bit := 1;            // 00 = 1; 01 = 8; 10 = 64; 11 = 256
  TCKPS_1_bit := 0;
  IPC0 := IPC0 or 0x1000;      // interrupt priority level = 1
  T1IF_bit := 0;               // clear T1IF
  T1IE_bit := 1;               // enable Timer1 interrupts
  TON_bit := 1;                // ON
  }


  {$IFDEF UART}
  UART1_Init(115200);
  Delay_ms(100);
  
  UART1_Write_Text('STALL_DISTRIBUTOR_PERIOD=');
  FloatToStr(STALL_DISTRIBUTOR_PERIOD, StrBuffer);
  UART1_Write_Text(StrBuffer);
  
  UART1_Write_Text('DWORD(STALL_DISTRIBUTOR_PERIOD/CLOCK_PERIOD)=');
  LongWordToStr(DWORD(STALL_DISTRIBUTOR_PERIOD/CLOCK_PERIOD), StrBuffer);
  UART1_Write_Text(StrBuffer);
  
  UART1_Write_Text('PRL=');
  WordToStr(CCP1PRL, StrBuffer);
  UART1_Write_Text(StrBuffer);
  UART_Write_Text('  PRH=');
  WordToStr(CCP1PRH, StrBuffer);
  UART_Write_Text(StrBuffer);
  UART1_Write(#13);
  {$ENDIF}
  
  // ALWAYS DO THIS LAST
  // Pin change Notification
  CN2IE_bit := 1;  // Pin Change Notification Interrupt on RA0
  CNIP_0_bit := 1;  // Interrupt priority = 7
  CNIP_1_bit := 1;
  CNIP_2_bit := 1;
  CNIF_bit := 0;    // Clear Pin Change Interrupt flag
  CNIE_bit := 1;    // Enable all Pin Change Interrupts

  
  while true do
  begin
    Delay_ms(100);
    {$IFDEF UART}
   UART1_Write_Text('RPM=');
    FloatToStr(Last_RPM_Global, StrBuffer);
    UART1_Write_Text(StrBuffer);
    UART_Write_Text('  State=');
    IntToStr(State, StrBuffer);
    UART_Write_Text(StrBuffer);
    UART1_Write(#13);
    {$ENDIF}
    // Waitin for Pin Change Interrupt
    HeartBeat_Pin := not HeartBeat_Pin;
  end;
end.